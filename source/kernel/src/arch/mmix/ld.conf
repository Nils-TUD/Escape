OUTPUT_FORMAT("elf64-mmix")
ENTRY(start)

PHDRS
{
	text	PT_LOAD;
	data	PT_LOAD;
	bss		PT_LOAD;
	regs	PT_LOAD;
}

SECTIONS
{
	. = 0x8000000000000000;
	
	.text : AT(ADDR(.text) - 0x8000000000000000)
	{
		*(.text*)
		*(.gnu.linkonce.t.*)
	} : text
	
	.rodata : AT(ADDR(.rodata) - 0x8000000000000000)
	{
		*(.rodata*)
		*(.gnu.linkonce.r.*)
	} : text

    .init_array : AT (ADDR (.init_array) - 0x8000000000000000)
    {
        PROVIDE (CTORS_BEGIN = .);
        KEEP (*(.init_array .ctors))
        PROVIDE (CTORS_END = .);
    } : data
	
	.data ALIGN (4096) : AT(ADDR(.data) - 0x8000000000000000)
	{
		*(.data)
		*(.data.*)
		*(.gnu.linkonce.d.*)
	} : data

	.bss ALIGN (4096) : AT(ADDR(.bss) - 0x8000000000000000)
	{
		*(COMMON*)
		*(.bss*)
		*(.gnu.linkonce.b.*)
	} : bss
	
	.MMIX.reg_contents :
	{
		/* Note that this section always has a fixed VMA - that of its first register * 8.  */
		*(.MMIX.reg_contents.linker_allocated);
		*(.MMIX.reg_contents);
	} : regs
}
